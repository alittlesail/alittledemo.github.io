
[Language(JavaScript)]
namespace JavaScript;

private var __JHTTPWX_MAXID:int = 0;

public class JHttpWxInterface : ALittle.IHttpSenderNative
{
    private int _id;
    private string _url;
    private string _content;
    private javascript.WxRequestTask _request;
    private javascript.WxResponseInfo _response;
    
    public ctor()
    {
        ++ __JHTTPWX_MAXID;
        this._id = __JHTTPWX_MAXID;
    }
    public fun GetID() : int { return this._id; }
    public fun SetURL(string url, string content)
    {
        this._url = url;
        this._content = content;
    }
    public fun Start()
    {
        var info = new javascript.WxRequestInfo();
        info.url = this._url;
        if (this._content == null)
            info.method = "GET";
        else
            info.method = "POST";
        if (this._content != null)
            info.data = this._content;
        info.success = bind(this.HandleCompleted, this);
        info.fail = bind(this.HandleError, this);
        this._request = window.wx.request(info);
    }
    public fun Stop()
    {
        if (this._request != null)
            this._request.abort();
    }
    public fun GetResponse() : string { return this._response.data; }                // 获取请求结果
    
    private fun HandleError()
	{
        ALittle.__ALITTLEAPI_HttpClientFailed(this._id, "wx.request failed");
	}
    
	private fun HandleCompleted(javascript.WxResponseInfo info)
	{
        this._response = info;
        ALittle.__ALITTLEAPI_HttpClientSucceed(this._id);
	}
}