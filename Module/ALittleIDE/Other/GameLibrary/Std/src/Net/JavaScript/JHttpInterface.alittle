
[Language(JavaScript)]
namespace JavaScript;

private var __JHTTP_MAXID:int = 0;

public class JHttpInterface : ALittle.IHttpSenderNative
{
    private int _id;
    private string _url;
    private string _content;
    private javascript.XMLHttpRequest _request;
    
    public ctor()
    {
        ++ __JHTTP_MAXID;
        this._id = __JHTTP_MAXID;
    }
    public fun GetID() : int { return this._id; }
    public fun SetURL(string url, string content)
    {
        this._url = url;
        this._content = content;
    }
    public fun Start()
    {
        this._request = new javascript.XMLHttpRequest();
        if (this._content == null)
            this._request.open("GET", this._url, true);
        else
            this._request.open("POST", this._url, true);
        
		var error_func = bind(this.HandleError, this);
        this._request.onerror = error_func;
		this._request.ontimeout = error_func;
        this._request.onload = bind(this.HandleCompleted, this);
        this._request.send(this._content);
    }
    public fun Stop()
    {
        if (this._request != null)
            this._request.abort();
    }
    public fun GetResponse() : string { return this._request.responseText; }                // 获取请求结果
    
    private fun HandleError()
	{
        ALittle.__ALITTLEAPI_HttpClientFailed(this._id, this._request.statusText);
	}
    
	private fun HandleCompleted()
	{
        ALittle.__ALITTLEAPI_HttpClientSucceed(this._id);
	}
}